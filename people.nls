;; people functions go here

to select-target-patch 
  ;; if agents are pushed harder than they are able to push, then they must select the patch in the direction of the force 
  ifelse magnitude pforce > push-force 
  [
    ; round push vector to cardinal direction, then set the patch in that direction as target 
    let push-heading cardinal-4 vector-direction pforce
    set target-patch patch-at-heading-and-distance push-heading 1   
  ]
  ;; else, select a target patch and move accordingly 
  [
    set target-patch max-one-of neighbors4 [ desirability ]
  ]
  
  ;; failsafe
  if target-patch = nobody [ set target-patch patch-here ]
  
end 

to attempt-move  
  (ifelse 
    ; if target patch isn't vacant, don't move
    any? [ people-here ] of target-patch or [pcolor] of target-patch = black [set obstructed? true]
  
    ; if target patch is not agent's current patch, then move 
    not (target-patch = patch-here)
    [ 
      update-dynamic-field    ; instruction to the patch the agent is on 
      face target-patch
      fd 1 
    ]
  )
end

to push       ;; vector add its pushing force to the force field value on the desired cell in the direction of desired travel 
  ; generate push force vector depending on direction agent is facing
  let push-vector scalar-product (list dx dy) push-force 
  
  ; vector add pushing force to patch ahead 
  if is-patch? patch-ahead 1 [ 
    ask patch-ahead 1 [
      set pforce vector-add push-vector pforce
    ]
  ]
end

to push-to-maintain-space      ;; TODO one quarter of the pushing force (kresist = 0.25 kpush) is vector-added into each adjacent occupied cell in the direction of that cell
  let maintain-push-force push-force / 4
  if xcor > min-pxcor and xcor < max-pxcor and ycor > min-pycor and ycor < max-pycor [
    ask patch-at-heading-and-distance 0 1 [ set pforce vector-add pforce list 0 maintain-push-force ]
    ask patch-at-heading-and-distance 90 1 [ set pforce vector-add pforce list maintain-push-force 0 ]
    ask patch-at-heading-and-distance 180 1 [ set pforce vector-add pforce list 0  (0 - maintain-push-force) ]
    ask patch-at-heading-and-distance 270 1 [ set pforce vector-add pforce list (0 - maintain-push-force) 0 ]
  ]
end 