to-report vector-add [ v1 v2 ]  ;; where input vector is a 2-element list. Courtesy of https://stackoverflow.com/questions/24962986/using-vectors-in-netlogo
  report (map + v1 v2)
end

to-report scalar-product [ v scalar ]
  report ( map [x -> x * scalar] v )
end

to-report magnitude [ v ]       ;; where input vector is a 2-element list
  report sqrt ( (item 0 v) ^ 2 + (item 1 v) ^ 2 )
end

to-report net-forces [ v4 ]     ;; converts the 4d total force vector ([NESW]) into a 2d net force vector ([xcomponent ycomponent])
  report (list (  item 1 v4 - item 3 v4 ) ( item 0 v4 - item 2 v4 ))
end

to-report vector-direction [ v ]
  report atan item 0 v item 1 v 
end

to-report cardinal-4 [ heading-input ]    ;; rounds heading into NESW headings, courtesy of https://stackoverflow.com/questions/46271318/netlogo-how-to-round-heading-into-90-180-270-360-only
  let dir heading-input
  let remain dir mod 90
  if remain != 0 [
    ifelse remain <= 45 [
      set dir dir - remain
    ]
    [
      set dir dir - remain + 90
    ]
  ]  
  report dir
end

to-report convert-heading-to-cardinal-index  [ heading-input ]
  let cardinal cardinal-4 heading-input
  report (floor (cardinal / 90)) mod 4
end